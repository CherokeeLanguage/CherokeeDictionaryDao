apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven'

group "com.github.CherokeeLanguage"
version = '20160801'

sourceCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

def RTJAR = '/usr/lib/jvm/java-7-openjdk-amd64/jre/lib/rt.jar';
if (new File(RTJAR).exists()) {
	compileJava.options.bootClasspath=RTJAR
}

repositories {
    jcenter()
	maven { url "https://jitpack.io" }
}

configurations {
	provided
}

dependencies {
	compile "com.mchange:c3p0:0.9.5.2"
	compile "org.mariadb.jdbc:mariadb-java-client:1.4.+"
	compile 'org.jdbi:jdbi:2.73'
	compile 'org.antlr:stringtemplate:3.2.1'
	compile "com.fasterxml.jackson.core:jackson-core:2.8.1"
	compile "com.fasterxml.jackson.core:jackson-databind:2.8.1"
	compile "com.fasterxml.jackson.core:jackson-annotations:2.8.1"
	
	compile 'com.github.CherokeeLanguage:CherokeeDictionaryShared:-SNAPSHOT'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	javadoc.failOnError=false
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
}

eclipseJdt << {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=utf-8')
    f = file('.settings/org.eclipse.core.runtime.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('line.separator=\\n\n')
}

project.afterEvaluate {
	// use jre lib matching version used by project, not the workspace default
	if (sourceCompatibility != null) {
		def target = project.sourceCompatibility.toString()
		def containerPrefix = "org.eclipse.jdt.launching.JRE_CONTAINER"
		def containerSuffix
		if (target =~ /1.[4-5]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/J2SE-' + target
		} else if (target =~ /1.[6-8]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-' + target
		}
		if (containerSuffix != null) {
			project.eclipse.classpath {
				containers.removeAll { it.startsWith(containerPrefix) }
				containers.add(containerPrefix + containerSuffix)
			}
		}
	}
}

eclipse {
	project {
		name = 'CherokeeDictionaryDao'
		//referencedProjects 'CherokeeDictionaryShared'
		natures 'org.eclipse.buildship.core.gradleprojectnature'
	}
	
	classpath {
		plusConfigurations += [configurations.provided]
		containers += ['org.springsource.ide.eclipse.gradle.classpathcontainer']
		downloadSources = true
		downloadJavadoc = true
	}

	jdt {
		sourceCompatibility=1.7
		targetCompatibility=1.7
	}

	wtp {
		facet {
			facets = []
			facet name: 'jst.java', version: '1.7'
			//facet name: 'jst.web', version: '3.0'
		}
	}

}

task "create-dirs" << {
	sourceSets*.java.srcDirs*.each { it.mkdirs() }
	sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}
